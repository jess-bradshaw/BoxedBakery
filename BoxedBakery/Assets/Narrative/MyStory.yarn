title: MyStory
tags:
position: -463,-327
---
//nothing here don't worry 
===
title: Start
tags:
position: -240,-327
---
<<wait 1>>
<<FadeIn EventSystem>>
<<DoorSound EventSystem>>
<i>Oh no, was that the door? I'm not ready.</i>
-> "Hello w-welcome!"
    ???: Don't be so nervous Bax, it's just me. 
-> "Welcome to my bakery what can I get you? 
    ???: Clear, warm, and an open ended question... 
    ???: Seems you have been practicing, Bax. 
-> ... 
    ???: Bax... you might want to greet customers. 
    ???: Not sure it's a great first impression.  
//EDIT THIS OUT LATER ------------------------------------
//-> Testing Option -Tutorial puzzle
//    <<FadeOut EventSystem>>
//    <<PackingView EventSystem>>
//    <<wait 1>>
//    <<jump Packing>>
//-> Testing Option -Puzzle 1
 //   <<FadeOut EventSystem>>
 //   <<PackingView EventSystem>>
 //   <<wait 1>>
 //   <<TutorialPuzzle EventSystem>>
//    <<BusyNoOrder EventSystem>>
//    <<Puzzle1 EventSystem>>
 //   <<jump Packing>> 
// -> Testing Option -Kid
   // <<set $Rea_NoCake += 1>>  
  //  <<FadeOut EventSystem>>
    //<<TutorialPuzzle EventSystem>>
    // <<wait 1>>
    //<<jump KidnDog>> 
 //-> Testing Option -Puzzle 2
 //   <<FadeOut EventSystem>>
 //   <<PackingView EventSystem>>
 //   <<wait 1>>
 //   <<TutorialPuzzle EventSystem>>
 //   <<KidOrder EventSystem>>
 //   <<Puzzle2 EventSystem>>
//    <<jump Packing>> 
//Break from testing area. --------------------
<<ReaEnabled EventSystem>> //First time Rea is on screen. 
<<ReaHappy EventSystem>>
<i>Of course that familiar mess of blonde hair would sneak in today. </i>
<<ReaEnabled EventSystem>>
Baxter: Hey Rea. 
-> Can I get you something before work?
    Rea: Good, but maybe rephrase that as 'What can I get you?' 
    Rea: Get the customer talking. 
    Baxter: Aye.
    Baxter: Rea you know I've done this before. 
    Rea: I know. Just reminding you. 
    Baxter: Feels different today...
    Rea: It is <b>different</b>. 
    Rea: This is <b>your</b> place, you can do what you want.  
    <<ReaHappy EventSystem>>
    Rea: Smile, wink, you know... be you. 
    <<ReaEnabled EventSystem>>
    <<ReaTip EventSystem>>
    <<jump Day1Smirk>>
-> Move you are blocking the door what if a <b>real</b> customer comes in?
    <<ReaWorried EventSystem>>
    Rea: I get that today is stressful but even for you that seemed harsh.
    Rea: Bax... you doing okay? 
    -> Baxter: Fine. Whatever just move. 
        Rea: Umm okay. I wanted to support you on the shop's first day.
        Baxter: ...
        Rea: C-can I get my regular? //She tells him but he doesn't write down the order. 
        Baxter: Fine. Just move. 
        <i>She shouldn't have come by, I don't need her here.</i>  
        <<set $Rea_Angry += 2>> 
        <<jump Day1Ignore>>
    -> Baxter: Sorry, I'm anxious about today.
        <<ReaEnabled EventSystem>>
        Rea: It's okay, that's why I wanted to visit before it got busy. 
        Rea: The place looks great, you look great. 
        <<ReaHappy EventSystem>>
        Baxter: Thanks Rea.
        Rea: You got this. 
        Rea: Plus you can get your first customer out of the way.
         <<ReaEnabled EventSystem>> 
        <i>She always knows what to say.</i> 
        <<jump Day1Smirk>>
-> ... 
    <<set $Rea_Angry += 1>> 
    Rea: Umm the shop looks nice. 
    Baxter: ...
     <<ReaEnabled EventSystem>>
    Rea: I thought I'd come support your grand opening.
    Baxter: ... 
    <<ReaWorried EventSystem>>
    Rea: S-sorry could I... get my regular?
    <<jump Day1Ignore>> // gets no info because you dont ask. 
===

title: Day1Smirk
position: -128,-116
---
<i>I'll never tell her, but I'm glad she stopped by.</i> 
Baxter: What can I get boxed up for you Sweet Buns? 
<<ReaHappy EventSystem>>
Rea: Oh boy. You haven't called me that in years. 
<<ReaEnabled EventSystem>>
Rea: You know my go to.
Baxter: One strawberry cupcake coming up. 
Rea: And make it official, I want to be the first paying customer of your own shop. 
<i>Of course she would. I'll even write it down to make her happy.</i> 
 <<ReaHappy EventSystem>>
<<ReaBigTip EventSystem>>
<<FadeOut EventSystem>>
<<PackingView EventSystem>>
<<ReaOrder EventSystem>>
<<wait 1>>
<<jump Packing>>
===

title: Day1Ignore
position: -334,-118
---
<<ReaWorried EventSystem>>
<i>Why is she looking at me like that? She just wants free food again.</i>
Rea: You know you could still... write...
Baxter: ... 
Rea: Okay sorry. 
<<FadeOut EventSystem>>
<<PackingView EventSystem>>
<<wait 1>>
<<jump Packing>>
===

title: Packing
position: -242,69
---
<<FadeIn EventSystem>>
===

title: BaxKnowsBetter
position: 22,66
---
//Triggered when player puts the wrong item is the box. 
<<hideButton EventSystem>>
<<if $Rea_Angry > 1>>
    Rea: That isn't... nevermind. 
<<else>>
    Rea: Umm Bax, my regular is a bit more colourful... 
<<endif>>
<<hideButton EventSystem>>
===

title: Day1ReaOrderRight
position: -251,264
---
<i>One order complete.</i>
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<ReaEnabled EventSystem>>
<<wait 1>>
<<FadeIn EventSystem>>
<<if $Rea_Angry > 0>>
    <<ReaWorried EventSystem>>
    Rea: Thanks Bax, I hope I didn't throw you off coming in. 
    Baxter: Nah sorry about earlier.
    Baxter: Been so focused getting ready. I didn't really sleep much last night. 
    Baxter: I couldn't figure out what items to offer, then I made too much.
    Baxter: Or maybe not enough? I'm not sure people will even show up. 
    <<ReaEnabled EventSystem>>
    Rea: Of course they will! It's not like they don't know your skills. 
    Rea: Just a change of location. Your own location. 
    Baxter: True... but what if they don't?
    Rea: Deep breath, they will. 
    Rea: You got this. Call me later if you want. 
    Baxter: Sounds good, have a good day at work. 
    //Track that Bax can call her
<<else>>
    <<ReaHappy EventSystem>>
    Rea: Yasss. Perfect! 
    <<ReaEnabled EventSystem>>
    Rea: Beautiful new shop, delicious cupcake. You got this!
    Baxter: Thanks Rea, really appreciate it. I'll call you later.
    Rea: Course, later! 
    //Track that Rea calls him. 
<<endif>>
<<FadeOut EventSystem>>
<<DoorSound EventSystem>>
<<ReaHide EventSystem>>
<<FadeIn EventSystem>>
<<TutorialPuzzle EventSystem>>
<i> That cupcake has no chance of surviving the trip to work.</i>
<i> I hope she's right about today.</i>
 <<FadeOut EventSystem>>
<<wait  2>>
<<jump Day1Business>>
===

title: Day1ReaOrderWrong
position: -10,271
---
<i>I don't count this as a real order. I make cupcakes all the time for her.</i>
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<ReaWorried EventSystem>>
<<wait 1>>
<<FadeIn EventSystem>>
<<if $Rea_Angry > 1>>
<<ReaAngry EventSystem>>
    <<set $Rea_NoCake += 1>>
    Rea: Keep it. 
    Rea: I was just trying to help.
    Rea: Seems you can't even remember your best friend's order.
    <<FadeOut EventSystem>>
    <<DoorSound EventSystem>>
    <<ReaHide EventSystem>>
    <<wait 1>>
    <<FadeIn EventSystem>>
    Baxter: Oh... 
        -> <i>I didn't need her storming in here anyways.</i>
        -> <i>I need a moment.</i>
            <i>I feel bad. I know she was trying to help.</i>
            <i>The place still is a mess and I barely have anything in stock.</i> 
            <i>Maybe I should call her later to appologize.</i>
                //Track that Bax will call her. 
    Baxter: I should put that back before someone else comes in. 
<<else>>
    Rea: Umm thanks Bax, I... hope your day goes smoothly. 
    Rea: S-sorry if me stopping in caused stress. Good luck today, it will be good.
    Baxter: ...  
    <<FadeOut EventSystem>>
    <<DoorSound EventSystem>>
    <<ReaHide EventSystem>>
    <<wait 1>>
    <<FadeIn EventSystem>>
    <i> Now what was I doing before she came in?</i> 
<<endif>>
<<FadeOut EventSystem>>
<<wait  2>>
<<TutorialPuzzle EventSystem>>
<<jump Day1Business>>
===
title: Day1Business
position: -138,492
---
// ---------------------------------------------------------Busy Starts---------------------------------------------
<<FadeIn EventSystem>>
<i>There's been a steady flow since Rea left.</i>
<i>It's been good. A few familiar faces. This shop might just survive.</i>
<i> I just need to tuck a few things...</i>
<<DoorSound EventSystem>>
<<wait 1>>
<<BusyEnabled EventSystem>>
???: No that won't work, we can't move the deadline. 
???: No... No. I can't. 
Baxter: Welcome-
???: I said NO.
???: Mmhm. That's fine. 
-> "Hello welcome"
    ???: No, say that again I couldn't hear you. 
-> <i>I'll just wave</i> 
    <<BusyTip EventSystem>>
    ???: Ahh yeah move it to then. 
    ???: Just move the other one instead.
-> <i>I should wait until they're off the phone.</i>
    <<BusyTip EventSystem>>
    Baxter: ...
    ???: I'll take my call then. 
    Baxter: ...
    ???: Just move the other one instead. 
???: Yeah, okay. No, I'm out grabbing the team food.
Baxter: ... 
???: Nah, some new shop... don't know.
<<BusyFour EventSystem>>
<i>Why are they holding up four fingers?</i> 
<<BusyCircle EventSystem>>
<i>Now a circle? What does that mean?</i> 
<<BusyEnabled EventSystem>>
???: Just put my order- No that meeting isn't for an hour... 
<i>Wait, what?! That was an order?</i>
-> <i>Bagels?</i>
    <<FadeOut EventSystem>>
    <<PackingView EventSystem>>
    <<BusyBagelOrder EventSystem>>
    <<wait 1>>
    <<Puzzle1 EventSystem>>
    <<jump Packing>>
-> <i>Donuts?</i>
    <<FadeOut EventSystem>>
    <<PackingView EventSystem>>
    <<BusyDonutOrder EventSystem>>
    <<BusyBigTip EventSystem>>
    <<wait 1>>
    <<Puzzle1 EventSystem>>
    <<jump Packing>>
-> <i>I should interrupt them to clarify what the order was.</i>
    Baxter: Sorry, could I-
    ???: Ugh. Say that again? 
    <i> I guess I'll pick whatever then.</i>  
    <<FadeOut EventSystem>>
    <<PackingView EventSystem>>
    <<BusyNoOrder EventSystem>>
    <<wait 1>>
    <<Puzzle1 EventSystem>>
    <<jump Packing>>
-> <i>I should wait until they give me full attention.</i>
    ???: ...Have Lars look into that...
    Baxter: ...
    ???: Sure. I know she's busy... she's our expert. 
    Baxter: ... 
    ???: Yeah. Check with her. 
    ???: This shop has Taiyaki, I'll grab some for her. 
    <i>How did they spot that? </i>
    <i>They have barely looked around.</i>
    ???: Yeah just some donuts for the team. 
    <i>Donuts...</i> 
    <i>Could have at least pointed to them.</i>
    <<BusyBigTip EventSystem>>
    <<set $Lars = true>>
    <<Lars EventSystem>>
    <<FadeOut EventSystem>>
    <<PackingView EventSystem>>
    <<BusyOrder EventSystem>>
    <<wait 1>>
    <<Puzzle1 EventSystem>>
    <<jump Packing>>
===
title: BusyTooBusy
position: 104,683
---
//Triggered when player puts the wrong item is the box. 
<<hideButton EventSystem>>
???: Move that to the next day... No, the other one... Yeah that meeting.... 
<<hideButton EventSystem>>
===
title: BusyCorrectOrder
position: -189,849
---
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<wait 1>>
<<BusyEnabled EventSystem>>
<<FadeIn EventSystem>>
???: Yeah, on my way. These look good. 
???: Ha. Maybe next time you're in town. 
???: I'll grab you a business card don't worry. 
<<FadeOut EventSystem>>
<<DoorSound EventSystem>>
<<wait 1>>
<<BusyHide EventSystem>>
<<FadeIn EventSystem>>
Baxter: That... was something.
<i>At least they might come back?</i> 
<i>Donuts... honestly I wish my old boss made us donuts.</i> 
<<set $BusCard = true>>
<<FadeOut EventSystem>>
<<Puzzle1Remove EventSystem>>
<<wait 1>>
<<jump KidnDog>>
===

title: BusyIncorrectOrder
position: 39,853
---
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<wait 1>>
<<BusyEnabled EventSystem>>
<<FadeIn EventSystem>>
???: Ugh, nah the shop messed it up. 
???: I don't have time for this, team gets what they get.
<<if $Lars == false>>
???: Get Kiba to grab something for Lars on his way in.
???: We want her on our side. 
<<else>>
???: At least they got the Taiyaki right.
???: They look alright I guess.  
<<endif>>
???: Mmhm. We can move that to tomorrow.
<<FadeOut EventSystem>>
<<DoorSound EventSystem>>
<<wait 1>>
<<BusyHide EventSystem>>
<<FadeIn EventSystem>>
Baxter: Well that was... something. 
<i>I wish they would have just stopped for a moment to tell me what they wanted.</i> 
<i>I'll do better with the next customer.</i> 
<<set $BusCard = false>>
<<FadeOut EventSystem>>
<<Puzzle1Remove EventSystem>>
<<wait 1>>
<<jump KidnDog>>
===
title: KidnDog
---
// ---------------------------------------------------------Kid & Dog Starts---------------------------------------------
<<DoorSound EventSystem>>
<<FadeIn EventSystem>>
<<wait 1>>
<i> Umm I swore I heard the door. </i>
<<KidEnabled EventSystem>>
Kid: Mister baker?
Baxter: Hey, name's Baxter.
Baxter: What's yours? And who's your friend?
Kid: I'm not allowed to tell strangers my name.
<<KidTail EventSystem>>
Kid: But this is Buddy! 
Baxter: Well nice to meet you and Buddy. 
<<KidEnabled EventSystem>>
Baxter: How can I help the two of you? 
<<if $Rea_NoCake > 0>> 
    Kid: There was this lady talking to herself about cupcakes. 
    Kid: We wanted to find it to see why she seemed so upset. 
    Kid: Do you make cupcakes that make people cry? 
    Baxter: I do make cupcakes but they don't make people cry...
    Kid: Hmm I think we need to try one to see if that's true.
    <<KidTail EventSystem>> 
<<else>>
    Kid: Well there was this lady eating a cupcake earlier. 
    Kid: We spent <b>all day</b> looking for it. 
    <<KidTail EventSystem>>
    Kid: Buddy are those the same ones? 
    Buddy: Bork! Bork! 
    Kid: Buddy says so.
<<endif>>
Kid: One vanilla cupcake for Buddy. 
<<KidEnabled EventSystem>>
Kid: One chocolate one for me. 
Kid: Two moon looking bread things for our parents. 
Baxter: Croissant? 
Kid: Yeah. Those things. 
<<KidTail EventSystem>>
Kid: Buddy's and mine can't touch.
Kid: Dogs can't have chocolate mister. 
<<FadeOut EventSystem>>
    <<PackingView EventSystem>>
    <<KidOrder EventSystem>>
    <<wait 1>>
    <<Puzzle2 EventSystem>>
    <<jump Packing>>

===
title: KidCorrection
---
Kid: Mister, don't let the chocolate touch Buddy's snack. 
===
title: KidCorrectOrder
---
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<wait 1>>
<<KidTail EventSystem>>
<<FadeIn EventSystem>>
Kid: Thanks mister! 
Buddy: Bork! 
<i> What should I say?<i>
-> "You are most welcome."
-> "Not sure cupcakes are best for dogs."
-> "Please leave Buddy outside next time."
Kid: Cupcakeeeeees! 
<<FadeOut EventSystem>>
<<DoorSound EventSystem>>
<<wait 2>>
<<KidHide EventSystem>>
<<jump EndofDay1>>
===
title: KidIncorrectOrder
--- 
<<FadeOut EventSystem>>
<<CustomerView EventSystem>>
<<wait 1>>
<<KidTail EventSystem>>
<<FadeIn EventSystem>>
Kid: Look Buddy! We got them! 
Kid: Oh.. umm. I see chocolate on yours...
<<KidEnabled EventSystem>>
Kid: Noooo!!! 
Kid: Day ruined.  
<<FadeOut EventSystem>>
<<DoorSound EventSystem>>
<<wait 2>>
<<KidHide EventSystem>>
<<jump EndofDay1>>
===
title: EndofDay1
position: -163,1097
---
<<Day1End EventSystem>>
<<FadeIn EventSystem>>
// -------------------------------------End of day 1 --------------------
<i>Door locked just need to pack up anything that won't keep till tomorrow.</i> 
//Check if rea is going to call or you call rea 
<i>I should give Rea an update.</i>
<<PhoneSound EventSystem>>
<<wait 2>>
Baxter: Rea? 
Rea: Hey Bax how did it go? 
Baxter: Pretty successful grand opening. 
Rea: That's great to hear, I knew you would do well!
<i>It was a busy day. What should I tell her?</i> 
-> "Some person wouldn't get off their phone."
    Rea: Well that's rude. 
    Baxter: Yeah. I know. It was a bit frustrating.
    <<if $BusCard == true>>
    Baxter: Sounded like they might be back so hopefully that goes better. 
    Rea: Yeah... hopefully! 
    <<else>>
    Baxter: Yeah I hope I don't see them again. 
    Rea: Seriously, like who does that?
    Baxter: Just pause the conversation or don't show up until your done your call. 
    Rea: Exactly, it's not hard to do. 
    <<endif>>
-> "Only a few strawberry cupcakes left."
    <<if $Rea_NoCake > 0>> 
    Rea: Oh... yeah?
    Baxter: I'll bring you one to make up for my mistake earlier. 
    Rea: Aww thanks Bax. It's okay, you were under a lot of pressure. 
    <<else>>
    Rea: I want all of them. 
    Baxter: Ha, they will keep till tomorrow. 
    Baxter: You're welcome to come pay for another...
    Rea: What?! I have to pay any time I want one now?
    Baxter: Fine fine, I'll bring you another. 
    <<endif>>
    Baxter: A kid and their dog that came in because of you.
    Rea: How many strawberry did they take?  
    Baxter: Nah they didn't take any strawberry ones. 
    Rea: Does this mean I get two since I brought in business?
    Baxter: Ha. Sure Rea. I'll pack another one up. 
-> "Thanks for coming by earlier."
    <<if $Rea_Angry  > 1>> 
     Rea: Of course.
     Baxter: I'm sorry for how I treated you. 
     Baxter: I know you were trying to help. 
     Rea: Awe, it's okay. I could tell something was off. 
    <<else>> 
     Rea: I wouldn't have missed it. 
     Baxter: It helped. 
     Rea: Oh yeah? 
     Baxter: I wouldn't say no to a regular every morning... 
     Rea: That sounds expensive.. 
     Baxter: Haha okay fine, a few freebies here and there. 
     Rea: Deal.
    <<endif>>
Rea: You coming over for dinner? 
Baxter: Sure just closing up for the day. 
Baxter: A bit of prep for tomorrow then I'll head over.
Rea: Perfect, I'll have a drink waiting for you. 
Rea: Successful first day!  
Baxter: Mmhm. See you soon. 
<<FadeOut EventSystem>>
<<wait 2>>
<<MainMenu EventSystem>>
//Add in something to fade out music so it can restart in the main menu
===
